%locations
%{
int yylex();
#include <stdio.h>
#include <string.h>
#include <stdlib.h>
#include <vector>
#include "include/json_rule_engine.h" 
extern int yylineno;
void yyerror (JsonRuleEngine *rule_engine, char *s);
%}

%code requires
{
#include <stack>
#include "include/expression_attribute.h" 
#include "include/generic_expression.h" 
#include "include/generic_condition.h" 
#include "include/statement.h" 
#include "include/json_rule_engine.h" 
#include "include/generic_comparator.h" 
#include "include/compare_value.h"

static void AddCodeBlock(Statement* stmt, std::vector<Statement*>* stmt_list)
{
	for(auto& st : *stmt_list)
	{
		stmt->add_statement(st);
	}
}

static std::stack<bool> g_trackCodeBlock;
}

%union 
{
	int64_t		sint64;
	double		doubleValue;
	bool		boolValue;
	char*		string;
	Identifier* ident;
	GenericExpression* genExpr;
	Statement* stmt;
	std::vector<Statement*>* stmtList;
	CompareOperators comparator;
	CompareValueBase* valueComparator;
};

%parse-param {JsonRuleEngine *rule_engine}

%token IN IF FOR END
%token DOUBLE_EQUAL GREATER_THAN_EQUAL GREATER_THAN LESS_THAN_EQUAL LESS_THAN 
%token JSON
%token PP
%token <string> IDENTIFIER
%token <string> QUOTED_STRING
%token <doubleValue> DOUBLE_VALUE
%token <boolValue> BOOL_VALUE
%token <sint64> SIGNED_VALUE
%token <string> DOT

%type <ident> STRING_IDENTIFIER
%type <genExpr> exp
%type <stmt> if_block for_block print_packet
%type <stmtList> code_block statements
%type <comparator> COMPARATOR
%type <valueComparator> COMPARING_VALUE
%start statements
%%

statements :
		   if_block
		   {
			   if (g_trackCodeBlock.size())
			   {
				   $$ = new std::vector<Statement*>();
				   $$->push_back($1);
			   }
			   else
			   {
				   rule_engine->GetCodeBlock().AddCodeBlock($1);
			   }
		   }
		   | for_block
		   {
			   if (g_trackCodeBlock.size())
			   {
				   $$ = new std::vector<Statement*>();
				   $$->push_back($1);
			   }
			   else
			   {
				   rule_engine->GetCodeBlock().AddCodeBlock($1);
			   }
		   }
		   | statements for_block
		   {
				if (!g_trackCodeBlock.size())
				{
					rule_engine->GetCodeBlock().AddCodeBlock($2);
				}
				else
				{
					$1->push_back($2);
				}
		   }
		   | statements if_block
		   {
				if (!g_trackCodeBlock.size())
				{
					rule_engine->GetCodeBlock().AddCodeBlock($2);
				}
				else
				{
					$1->push_back($2);
				}
		   }
		   | print_packet
		   {
			   $$ = new std::vector<Statement*>();
			   $$->push_back($1);
		   }
		   ;

print_packet :
			 PP { $$ = new print_packet_statement(); }
			 ;

if_block : 
		IF exp COMPARATOR COMPARING_VALUE START_BRACE code_block
		{
			$2->SetBlockType(1);
			$$ = new IfStatement();
			$4->SetComparator($3);
			Condition* ifCond = new IfCondition($2, $4, $$);
			$$->set_condition(ifCond);
			AddCodeBlock($$, $6);
			delete $6;
		}
		;

COMPARATOR :
	DOUBLE_EQUAL
	{
		$$ = CompareOperators::DoubleEqualsOperator;
	}
	|
	GREATER_THAN_EQUAL
	{
		$$ = CompareOperators::GreaterThanEqualsOperator;
	}
	|
	GREATER_THAN
	{
		$$ = CompareOperators::GreaterThanOperator;
	}
	|
	LESS_THAN_EQUAL
	{
		$$ = CompareOperators::LessThanEqualsOperator;
	}
	|
	LESS_THAN
	{
		$$ = CompareOperators::LessThanOperator;
	}
	;

COMPARING_VALUE :
	QUOTED_STRING
	{
		$$ = new CompareValue<std::string>($1, BASIC_DTYPE::STRING);
	}
	|
	SIGNED_VALUE
	{
		$$ = new CompareValue<int64_t>($1, BASIC_DTYPE::SINT64);
	}
	|
	BOOL_VALUE
	{
		$$ = new CompareValue<bool>($1, BASIC_DTYPE::BOOL);
	}
	|
	DOUBLE_VALUE
	{
		$$ = new CompareValue<double>($1, BASIC_DTYPE::DOUBLE);
	}
	;

for_block :
		 FOR IDENTIFIER IN exp START_BRACE code_block
		 {
			 $4->SetBlockType(2);
			 $$ = new ForStatement();
			 Condition* forCond = new ForCondition(std::string($2), $4, $$);
			 $$->set_condition(forCond);
			 AddCodeBlock($$, $6);
			 free($2);
			 delete $6;
		 }
		 ;

exp : JSON
	{
		$$ = new JsonExpression();
		$$->SetExpressionEvaluator(rule_engine->GetJsonParserImplementer()->GetJsonExprEvalImpl());
	}
	|
	JSON '[' SIGNED_VALUE ']'
	{
		$$ = new JsonExpression($3);
		$$->SetExpressionEvaluator(rule_engine->GetJsonParserImplementer()->GetJsonExprEvalImpl());
	}
	|
	STRING_IDENTIFIER
	{
		$$ = new NonJsonExpression();
		$$->AddIdentifier($1);
		$$->SetExpressionEvaluator(rule_engine->GetJsonParserImplementer()->GetJsonExprEvalImpl());
	}
	|
	exp STRING_IDENTIFIER
	{
		$$ = $1;
		$$->AddIdentifier($2);
	}
	;

STRING_IDENTIFIER :
				IDENTIFIER
				{
					$$ = new Identifier(std::string($1));
					free($1);
				}
				|
				DOT IDENTIFIER
				{
					$$ = new Identifier(std::string($2));
					free($2);
				}
				|
				STRING_IDENTIFIER '[' SIGNED_VALUE ']'
				{
					$$ = $1;
					if (!$$)
						$$ = new Identifier();
					$$->AddIndiceToLastExprAttribute($3);
				}
				;

START_BRACE: 
		   '{'
		   {
			   g_trackCodeBlock.push(true);
		   }
		   ;

code_block: 
		  END statements END END_BLOCK END
		  {
			  $$ = new std::vector<Statement*>();
			  for (auto& st: *$2)
			  {
			  	$$->push_back(st);
			  }
			  delete $2;
		  }
		  |
		  END statements END_BLOCK END
		  {
			  $$ = new std::vector<Statement*>();
			  for (auto& st: *$2)
			  {
			  	$$->push_back(st);
			  }
			  delete $2;
		  }
		  ;

END_BLOCK : 
	'}'
	{
		g_trackCodeBlock.pop();
	}
	;
%%


void yyerror ([[gnu::unused]]JsonRuleEngine *rule_engine, char* s) {
	fprintf (stderr, "%s\n", s);
}
